#!/usr/bin/env zsh

# manually upgrade `backblaze` cask for two reasons:
# - cached downloads of the zipball cause the SHA check to fail when fetching new versions
# - even for successful upgrades, Homebrew goes through an excessive full uninstall/reinstall process
#
# instead, we can:
# - perform the fetch -> attach -> caskroom copy process manually
# - use the built-in in-place upgrade command provided by the Backblaze Installer
# - mark the cask as upgraded by creating the appropriate metadata directory
function upgrade-backblaze {
  function log1() {
    echo -e "\033[34m=>\033[0m \033[1m$1\033[0m"
  }

  function log2_error() {
    echo -e "\033[31m=>\033[0m \033[1m$1\033[0m"
  }

  function log2() {
    echo -e "\033[34m==>\033[0m \033[1m$1\033[0m"
  }

  log1 "Performing manual Backblaze upgrade"

  sudo -v

  local version="$(brew info --cask --json=v2 backblaze | jq --raw-output '.casks[0].version')"
  local caskroom="$(brew --prefix)/Caskroom"

  # fetch zipball
  brew fetch --cask backblaze --force
  local zipball="$(brew --cache --cask backblaze)"

  # extract zipball contents into caskroom
  log2 "Extracting zipball contents into Caskroom"
  local version_dir="$caskroom/backblaze/$version"
  mkdir -vp "$version_dir"
  unzip -u "$zipball" -d "$version_dir"

  log2 "Success! Copied successfully to -> $version_dir"

  # perform upgrade
  log2 "Performing upgrade"
  sudo "$version_dir/bzdoinstall.app/Contents/MacOS/bzinstall_mate" -upgrade bzdiy || {
    log2_error "Manual Backblaze upgrade failed"
    return 1
  }

  log2 "Upgrade successful"

  log2 "Marking Cask 'backblaze' as upgraded in Homebrew metadata"

  # create metadata directory
  local timeformat="%Y%m%d%H%M%S.%L" # format used by Homebrew
  local rubyscript="puts Time.now.utc.strftime '$timeformat'"
  local time="$(ruby -e $rubyscript)"
  mkdir -vp "$caskroom/backblaze/.metadata/$version/$time/Casks/"

  # copy formula to metadata directory
  local formula="$(brew --prefix)/Homebrew/Library/Taps/homebrew/homebrew-cask/Casks/backblaze.rb"
  cp -v "$formula" "$caskroom/backblaze/.metadata/$version/$time/Casks/backblaze.rb"

  log2 "Homebrew metadata updated successfully"

  log2 "Performing cleanup"
  brew cleanup --verbose backblaze
  log2 "Cleanup finished successfully"

  log1 "Manual Backblaze upgrade successful"
}
